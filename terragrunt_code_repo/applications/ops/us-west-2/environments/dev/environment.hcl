# --------------------------------------------------------
# ENVIRONMENT TERRAGRUNT CONFIGURAION
# --------------------------------------------------------

locals {
    # Read hcl from region
    region_hcl_path = find_in_parent_folders("region.hcl")
    region_hcl_config = read_terragrunt_config(local.region_hcl_path)

    aws_provider_region       = try(local.region_hcl_config.locals.aws_provider_region, "us-west-2")
    aws_profile               = "dev"
    account_id                = "your-acct-#"
    vpc_name                  = "playground-vpc"
    vpc_public_subnet_filter  = "${local.vpc_name}-public-*"
    vpc_private_subnet_filter = "${local.vpc_name}-private-*"
    environment_name          = "${reverse(split("/", get_path_from_repo_root()))[0]}"

    # Read hcl from platform
    platform_hcl_path = find_in_parent_folders("platform.hcl")
    platform_hcl_config = read_terragrunt_config(local.platform_hcl_path)

    cost_center               = try(local.platform_hcl_config.locals.cost_center, "unknown")
    platform_name             = try(local.platform_hcl_config.locals.platform_name, "unknown")
    resource_owner            = try(local.platform_hcl_config.locals.resource_owner, "unknown")
    platform_locals           = local.platform_hcl_config.locals
}

generate "environment_locals" {
    path = "tg_environment_locals.tf"
    if_exists = "overwrite_terragrunt"
    contents = <<EOF
    #########################################################################################
    # Note: This file has been generated by Terragrunt using the terragrunt init command.   #
    # Any modifications made to this file may be overwritten the next time terragrunt init  #
    # is executed.                                                                          #
    #########################################################################################
    locals {
        stack_name = basename(abspath(path.module))
    }
    EOF    
}
